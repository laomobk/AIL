
import 'time'


_DEFAULT_HP = 100
_writeln = console.writeln
_write = console.write

fun getPlayer() {
    return _player
}


class Player {
    fun __init__(self, name) {
        self.name = name
        self.hp = _DEFAULT_HP
    }
}


_player = Player('Laomo')


class Action {
    fun onSelect() {}
    fun getActionName() {}
    fun getActionDescription() {}
}


class MessageAction extends Action {
    fun __init__(self, name, message) {
        self.message = message
        self.name = name
    }

    fun onSelect(self) {
        _writeln('[M] %s'.format(self.message))
    }

    fun getActionName(self) {
        return self.name
    }

    fun getActionDescription(self) {
        return self.message
    }
}


class SectionAction extends Action {
    fun __init__(self, name, section) {
        self.name = name
        self.section = section
    }

    fun getActionName(self) {
        return self.name
    }

    fun getSection(self) {
        return self.section
    }

    fun getActionDescription(self) {
        return '[NextSection]'
    }
}


class CustomAction extends Action {
    fun __init__(self, onSelectFunc, name, description) {
        self.onSelectFunc = onSelectFunc
        self.name = name
        self.description = description
    }

    fun onSelect(self) {
        return self.onSelectFunc()
    }

    fun getActionName(self) {
        return self.name
    }

    fun getActionDescription(self) {
        return self.description
    }
}


class Section {
    fun start(self) {}
    fun getActionList(self) {}
}


class SectionBegin extends Section {
    fun start(self) {
        _writeln('> You are now in a dark cave!')
        _writeln('> There is a ghost in front of you!')
    }

    fun getActionList(self) {
        return [SectionAction('GoFight', SectionFight())]
    }
}


class SectionFight extends Section {
    fun start(self) {
        p = getPlayer()

        while p.hp > 0 {
            _writeln('> A ghost front of you!')
            _writeln('> you are attacked! HP -1')
            p.hp -= 1
            time.sleep(0.01)
        }

        _writeln('> game over!')
    }
}


fun askForAction(aList) {
    while true {
        try {
            x = console.readln('select:')
            x = int(x)
            if x >= 0 and x < len(aList) {
                return aList[x]
            }
            print '[E] please enter right number'
        } catch _ {
            print '[E] please enter a number'
        }
    }
}


fun runGame() {
    section = SectionBegin()

    section.start()
    ac = section.getActionList()
    ac.forEach(fun (i, action) {
        _writeln('[%s] %s: %s'.format(
                    i, action.getActionName(), action.getActionDescription()))
    })
    sac = askForAction(ac)
    if isinstance(sac, SectionAction) {
        sac.section.start()
    }
}


if __main__ {
    runGame()
}



